import:
	java.util.ArrayList
	java.util.HashMap
	org.bukkit.block.Chest
	org.bukkit.craftbukkit.v1_12_R1.block.CraftSkull
	org.bukkit.event.block.BlockBreakEvent
	org.bukkit.Material


on death:
	victim is not a player
	event.getDrops().clear()
	if victim has scoreboard tag "isPillar":
		victim has metadata "attrMap"
		set {_fragment} to name of victim
		replace all "柱" in {_fragment} with "碎片"
		set {_fragment} to getItem({_fragment})
		set {_amount} to a random integer between 75 and 125
		loop {_amount} times:
			drop {_fragment} at (location of victim).add((a random number between 5 and -5), (a random number between 8 and -8), (a random number between 5 and -5)) without velocity
			set gravity of (dropped item) to off
	else:
		max health of victim > 1
		victim doesn't have metadata "damageTaker"
		scoreboard tags of victim doesn't contain "healthPool"
		set {_tool} to tool of victim
		if name of {_tool} is set:
			victim is zombie or husk or skeleton or stray
			event.getDrops().add({_tool})
		set {_motherType} to trimText(name of victim)
		if victim has metadata "motherType":
			set {_motherType} to metadata "motherType" of victim
		set {_type} to trimText(name of victim)
		if {_type} is "神圣宝箱怪" or "腐化宝箱怪":
			if chance of 1/6:
				set {_item} to getItem("魔法箭袋")
			if chance of 1/5:
				set {_item} to getItem("再生手环")
			else if chance of 1/4:
				set {_item} to getItem("泰坦手套")
			else if chance of 1/3:
				set {_item} to getItem("炼金石")
			else if chance of 1/2:
				set {_item} to getItem("十字项链")
			else:
				if {_type} is "神圣宝箱怪":
					set {_item} to getItem("代达罗斯风暴弓")
				else:
					set {_item} to getItem("腐香囊")
			event.getDrops().add({_item})
		set {_drop} to yml value "%{_motherType}%.drop" from file "plugins/Data/entities.yml"
		{_drop} is set
		set {_iterator} to {_drop}.iterator()
		set {_moneyTotal} to 0
		while {_iterator}.hasNext():
			clear {_info::*}
			set {_info::1} to {_iterator}.next()
			set {_info::*} to {_info::1} split at ":"
			if chance of {_info::4} parsed as number:
				set {_amount} to a random integer between {_info::2} parsed as number and {_info::3} parsed as number
				if {_info::1} is not "铜币":
					set {_item} to {_amount} of getItem({_info::1})
					if {_info::1} is "飞翔之魂" or "诅咒焰":
						drop {_item} at (eye location of victim) without velocity
						set gravity of (dropped item) to off
					else:
						event.getDrops().add({_item})
				else:
					add {_amount} to {_moneyTotal}
		if {_motherType} is not {_type}:
			set {_drop} to yml value "%{_type}%.drop" from file "plugins/Data/entities.yml"
			{_drop} is set
			set {_iterator} to {_drop}.iterator()
			while {_iterator}.hasNext():
				clear {_info::*}
				set {_info::1} to {_iterator}.next()
				set {_info::*} to {_info::1} split at ":"
				if chance of {_info::4} parsed as number:
					set {_amount} to a random integer between {_info::2} parsed as number and {_info::3} parsed as number
					if {_info::1} is not "铜币":
						set {_item} to {_amount} of getItem({_info::1})
						if {_info::1} is "飞翔之魂" or "诅咒焰":
							drop {_item} at (eye location of victim) without velocity
							set gravity of (dropped item) to off
						else:
							event.getDrops().add({_item})
					else:
						add {_amount} to {_moneyTotal}
		if {_moneyTotal} > 0:
			dropMoney((eye location of victim), {_moneyTotal})

on BlockBreakEvent:
	set {_block} to event.getBlock()
	set {_newBlock} to air
	set {_material} to "%{_block}.getType()%"
	if {_material} is "CHEST" or "TRAPPED_CHEST":
		set {_inv} to (block at location of {_block}).getState().getBlockInventory()
		set {_contents} to {_inv}.getStorageContents()
		loop ...{_contents}:
			clear {_item}
			set {_item} to loop-value
			{_item} is set
			cancel event
			stop trigger
	set {_attrMap} to metadata "attrMap" of player
	set {_pickaxePower} to {_attrMap}.getOrDefault("powerPickaxe", 0)
	set {_altered} to true
	set {_dropLoc} to {_block}.getLocation().add(0.5, 0.4, 0.5)
	set {_biome} to getBiome(location of {_block})
	set {_itemDefault} to yml value ({_material}) from file "plugins/Data/itemMap.yml"
	if {_itemDefault} is set:
		add getItem({_itemDefault}) to {_dropItem::*}
	if {_material} is "MAGMA":
		if {_pickaxePower} < 60:
			cancel event
			stop trigger
		add getItem("狱炎矿") to {_dropItem::*}
		set {_newBlock} to flowing lava
	else if {_material} is "STONE":
		if {_biome} is "tundra":
			clear {_dropItem::*}
			add getItem("冰块") to {_dropItem::*}
		if {_biome} is "hallow":
			metadata "tier" of player >= 4
			if chance of 0.075:
				if chance of 0.05:
					add getItem("明胶水晶") to {_dropItem::*}
				else:
					add getItem("水晶碎块") to {_dropItem::*}
	else if {_material} starts with "LOG":
		set {_item} to getItem("木材")
		set {_item} to (a random integer between 1 and 4) of {_item}
		add {_item} to {_dropItem::*}
	else if {_material} is "BROWN_MUSHROOM" or "RED_MUSHROOM" or "HUGE_MUSHROOM_1" or "HUGE_MUSHROOM_2":
		if y-coordinate of {_dropLoc} < 50:
			add getItem("发光蘑菇") to {_dropItem::*}
		else if chance of 0.05:
			add getItem("发光蘑菇") to {_dropItem::*}
		else:
			add getItem("蘑菇") to {_dropItem::*}
	else if {_material} is "WOOL" or "OBSIDIAN":
		set {_altered} to false
	else if {_material} starts with "LEAVES":
		set {_altered} to false
	else if {_material} is "DIRT" or "GRASS" or "MYCEL" or "GRASS_PATH" or "SOIL":
		set {_altered} to false
		if {_material} is "GRASS" or "MYCEL":
			if {_biome} is "jungle":
				if y-coordinate of {_dropLoc} < 50:
					chance of 0.01
					add getItem("大自然的恩赐") to {_dropItem::*}
				if chance of 0.05:
					add getItem("毒刺") to {_dropItem::*}
				else if chance of 0.02:
					add getItem("丛林孢子") to {_dropItem::*}
			if chance of 0.05:
				if {_material} is "GRASS":
					add getItem("草种") to {_dropItem::*}
				else:
					add getItem("腐化种子") to {_dropItem::*}
			chance of 0.1
			if y-coordinate of {_dropLoc} < 30:
				add getItem("火焰花") to {_dropItem::*}
			else if {event} is "陨石雨":
				add getItem("火焰花") to {_dropItem::*}
			else if y-coordinate of {_dropLoc} < 50:
				add getItem("闪耀根") to {_dropItem::*}
			else if {event} is "血月":
				add getItem("死亡草") to {_dropItem::*}
			else if {_biome} is "corruption":
				add getItem("死亡草") to {_dropItem::*}
			else if {_biome} is "jungle":
				add getItem("月光草") to {_dropItem::*}
			else if {_biome} is "tundra":
				add getItem("寒颤棘") to {_dropItem::*}
			else if {_dropLoc}.getWorld().getTime() is not between 13500 and 22500:
				if chance of 0.2:
					add getItem("蘑菇") to {_dropItem::*}
				else:
					add getItem("太阳花") to {_dropItem::*}
			else if {_biome} is "desert":
				add getItem("波浪叶") to {_dropItem::*}
	else if {_material} ends with "_ORE":
		if "%world of {_block}%" is not "world_nether":
			loop 3 times:
				if {_material} is "COAL_ORE":
					set {_rand} to a random integer between 1 and 20
				else if {_material} is "IRON_ORE" or "REDSTONE_ORE" or "GLOWING_REDSTONE_ORE":
					set {_rand} to a random integer between 10 and 50
				else if {_material} is "LAPIS_ORE":
					set {_rand} to a random integer between 60 and 100
				else if {_material} is "EMERALD_ORE":
					add getItem("生命水晶") to {_dropItem::*}
					set {_rand} to 1000
					stop loop
				else:
					set {_rand} to a random integer between 30 and 80
					if {_material} is "DIAMOND_ORE":
						chance of 0.2
						add getItem("钻石") to {_dropItem::*}
						set {_rand} to 1000
				if chance of 0.3:
					(metadata "tier" of player) >= 4
					if {_biome} is "jungle":
						(metadata "tier" of player) >= 6
						chance of 0.35
						{_pickaxePower} >= 200
						add getItem("叶绿矿") to {_dropItem::*}
						set {_rand} to 1000
					if {_rand} < 35:
						{_pickaxePower} >= 100
						add getItem("钴矿") to {_dropItem::*}
						set {_rand} to 1000
					else if {_rand} < 60:
						{_pickaxePower} >= 110
						add getItem("秘银矿") to {_dropItem::*}
						set {_rand} to 1000
					else:
						{_pickaxePower} >= 150
						add getItem("精金矿") to {_dropItem::*}
						set {_rand} to 1000
				if {_rand} < 17:
					add getItem("铜矿石") to {_dropItem::*}
				else if {_rand} < 41:
					add getItem("铁矿石") to {_dropItem::*}
				else if {_rand} < 65:
					add getItem("银矿石") to {_dropItem::*}
				else if {_rand} < 101:
					add getItem("金矿石") to {_dropItem::*}
		else:
			if {_pickaxePower} < 60:
				cancel event
				stop trigger
			add getItem("狱炎矿:2") to {_dropItem::*}
			set {_newBlock} to flowing lava
	else if {_material} is "WORKBENCH":
		add getItem("木制工作台") to {_dropItem::*}
	else if {_material} is "BREWING_STAND":
		add getItem("酿造台") to {_dropItem::*}
	else if {_material} is "CHEST" or "TRAPPED_CHEST" or "ANVIL" or "FURNACE":
		set {_blockName} to trimText({_block}.getState().getInventory().getName().toString())
		set {_dropItemByName} to false
		if {_blockName} is set:
			set {_dropItemByName} to {Items}.containsKey({_blockName})
		if {_dropItemByName} is false:
			if {_material} is "CHEST" or "TRAPPED_CHEST":
				add getItem("箱子") to {_dropItem::*}
			else if {_material} is "ANVIL":
				add getItem("铁砧") to {_dropItem::*}
			else if {_material} is "FURNACE":
				add getItem("火炉") to {_dropItem::*}
		else:
			add getItem({_blockName}) to {_dropItem::*}
	else if {_material} is "SKULL":
		set {_UUID} to {_block}.getState().getOwningPlayer().getUniqueId().toString()
		if {_UUID} is "81834b91-513d-450f-b920-6880fd47c997":
			add getItem("生命果") to {_dropItem::*}
		else if {_UUID} is "50af25ef-adb9-44d0-880f-6890ea0dc182":
			handleSJZH(player)
	if {_altered} is true:
		set (block at {_block}) to {_newBlock}
		cancel event
	if {_material} is not "WOOL":
		{_material} is not "OBSIDIAN"
		wait 1 tick
		loop {_dropItem::*}:
			(world of {_block}).dropItemNaturally({_dropLoc}, loop-value)
			set {_dropItem} to dropped item
			{_dropItem}.setTicksLived(1)
		wait 1 second
		if {_newBlock} is flowing lava:
			set (block at {_block}) to air