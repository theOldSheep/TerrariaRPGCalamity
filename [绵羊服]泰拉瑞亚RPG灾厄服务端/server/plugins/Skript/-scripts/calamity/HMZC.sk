import:
	java.util.ArrayList
	java.util.HashMap
	net.minecraft.server.v1_11_R1.BossBattle
	net.minecraft.server.v1_11_R1.BossBattle$BarColor
	net.minecraft.server.v1_11_R1.BossBattle$BarStyle
	net.minecraft.server.v1_11_R1.BossBattleServer
	net.minecraft.server.v1_11_R1.PacketPlayOutBoss$Action
	org.bukkit.craftbukkit.v1_11_R1.util.CraftChatMessage
	org.bukkit.entity.EntityType

on rightclick:
	gamemode of player is survival
	set {_tool} to player's tool
	if {_tool}.getType().toString() is "EYE_OF_ENDER" or "ENDER_PEARL":
		cancel event
	set {_toolOffhand} to player's offhand tool
	if {_toolOffhand}.getType().toString() is "EYE_OF_ENDER" or "ENDER_PEARL":
		cancel event
	metadata "useCD" of player is false
	if trimText(name of {_tool}) is "可疑的眼球":
		player.getWorld().getTime() is between 13500 and 22500
		{BOSS}.containsKey("克苏鲁之眼") is false
		spawn a slime 41 behind player
		handleEOC(spawned entity, player)
		applyCD(player, 20)
		set {_tool} to (item amount of {_tool} - 1) of {_tool}
		set player's tool to {_tool}

function handleEOC(eoc: entity, ply: player):
	broadcast "&d&l克苏鲁之眼 苏醒了！"
	play sound "entity.enderdragon.growl" with volume 10 and pitch 1 at location of {_eoc}
	set {_attrMap} to new HashMap()
	{_attrMap}.put("crit", 0.04)
	{_attrMap}.put("damage", 60)
	{_attrMap}.put("damageMeleeMulti", 1)
	{_attrMap}.put("damageMulti", 1)
	{_attrMap}.put("damageType", "Melee")
	{_attrMap}.put("defence", 24)
	{_attrMap}.put("defenceMulti", 1)
	{_attrMap}.put("knockback", 4)
	{_attrMap}.put("knockbackResistance", 1)
	{_attrMap}.put("knockbackMeleeMulti", 1)
	{_attrMap}.put("knockbackMulti", 1)
	set metadata "attrMap" of {_eoc} to {_attrMap}
	
	set {_bossbar} to new BossBattleServer(CraftChatMessage.fromString("克苏鲁之眼", true)[0], BarColor.GREEN, BarStyle.PROGRESS)
	{_bossbar}.setVisible(true)
	set metadata "bossbar" of {_eoc} to {_bossbar}
	
	set {_targets} to setupBossTarget({_eoc}, 0, {_ply}, true, {_bossbar})
	set metadata "targets" of {_eoc} to {_targets}
	set {_healthMulti} to getBossHealthMulti({_targets}.size())
	{_eoc}.setInvulnerable(true)
	set name of {_eoc} to "克苏鲁之眼"
	{_eoc}.setCustomName("克苏鲁之眼")
	play sound "entity.enderdragon.growl" with volume 10 and pitch 1 at location of {_eoc}
	add "isMonster" to scoreboard tags of {_eoc}
	add "isBOSS" to scoreboard tags of {_eoc}
	{_eoc}.setCustomNameVisible(true)
	
	{_eoc}.setGravity(false)
	{_eoc}.setRemoveWhenFarAway(false)
	{_eoc}.setSize(5)
	
	{BOSS}.put("克苏鲁之眼", {_eoc})
	
	wait 1 tick
	{_eoc}.setInvulnerable(false)
	set max health of {_eoc} to round(7280 * {_healthMulti})
	set health of {_eoc} to round(7280 * {_healthMulti})
	
	set {_rage} to -1
	set {_indexAI} to 0
	set {_typeAI} to "CHARGE"
	set {_countAI} to 0
	set {_target} to {_ply}
	set metadata "target" of {_eoc} to {_target}
	
	renderSingleBossbar({_eoc}, {_bossbar})
	while health of {_eoc} > 0:
		{_eoc}.setVelocity({_eoc}.getVelocity().zero())
		set {_loc} to {_eoc}.getLocation()		
		
		#clear invalid targets
		if {_target} is set:
			checkBossTarget({_target}, {_eoc}, false) is false
			clear {_target}
			clear metadata "target" of {_eoc}
		
		if {_eoc}.getWorld().getTime() is not between 13500 and 22500:
			clear {_target}
			clear metadata "target" of {_eoc}
			#flee under daylight
			{_loc}.add(0, 5, 0)
			set {_eoc}.getHandle().pitch to -90
			if {_loc}.getY() >= 255:
				set max health of {_eoc} to 1
				{_eoc}.remove()
				stop loop
		else:
			if {_target} is not set:
				loop toSkList {_targets}.keySet():
					set {_ply} to "%loop-value%" parsed as player
					checkBossTarget({_ply}, {_eoc}, false) is true
					set {_target} to {_ply}
					set metadata "target" of {_eoc} to {_target}
					makeTarget({_eoc}, {_target})
					stop loop
				#find new target if avaliable, otherwise flee
			if {_target} is not set:
				{_loc}.add(0, 1.25, 0)
				set {_eoc}.getHandle().pitch to -90
				if {_loc}.getY() >= 255:
					set max health of {_eoc} to 1
					{_eoc}.remove()
					stop loop
			else:
				#AI part starting here
				if {_rage} is -1:
					if (health of {_eoc} / max health of {_eoc}) <= 0.6:
						set {_rage} to 17
						set {_bossbar}.color to BarColor.YELLOW
						{_bossbar}.sendUpdate(Action.UPDATE_STYLE)
						set name of {_eoc} to "克苏鲁之眼&1"
						{_eoc}.setCustomName("克苏鲁之眼&1")
					if {_typeAI} is "CHARGE":
						#normal charge
						if {_indexAI} <= 17:
							if {_indexAI} is 0:
								set {_dVec} to ({_target}.getLocation().subtract({_eoc}.getLocation())).toVector()
								play sound "entity.enderdragon.growl" with volume 10 and pitch 1 at {_loc}
								{_dVec}.normalize().multiply(max(2, distance between {_target} and {_eoc} / 13))
							
							{_loc}.add({_dVec})
							
						else:
							#charge * 3, then spawn little EOC's
							add 1 to {_countAI}
							set {_indexAI} to -1
							if {_countAI} > 3:
								set {_countAI} to 0
								set {_typeAI} to "SUMMON"
					else if {_typeAI} is "SUMMON":
						#not summoning until it moves above player
						if mod({_indexAI}, 3) = 0:
							set {_dVec} to ({_target}.getLocation().add(0, 15, 0).subtract({_eoc}.getLocation())).toVector()
							set vector length of {_dVec} to sqrt(vector length of {_dVec})
						{_loc}.add({_dVec})
						if (y-coordinate of {_eoc}) - (y-coordinate of {_target}) >= 10:
							if {_indexAI} >= 17:
								set {_indexAI} to 0
								add 1 to {_countAI}
								#summoning
								
								spawnMob("克苏鲁的仆从", {_eoc}.getLocation().add(0, -4, 0), {_target})
								
								if {_countAI} >= 3:
									if {_countAI} = 3:
										if chance of 0.5:
											set {_countAI} to 0
											set {_typeAI} to "CHARGE"
									else:
										set {_countAI} to 0
										set {_typeAI} to "CHARGE"
				else if {_rage} >= 0:
					if {_rage} > 0:
						#spin and release massive amount of servants
						if mod({_rage}, 3) = 0:
							spawnMob("克苏鲁的仆从", {_eoc}.getLocation(), {_target})
						remove 1 from {_rage}
						if {_rage} = 0:
							{_attrMap}.put("damage", 72)
							{_attrMap}.put("defence", 0)
							set {_bossbar}.color to BarColor.RED
							{_bossbar}.sendUpdate(Action.UPDATE_STYLE)
							spawnMob("克苏鲁的仆从", {_eoc}.getLocation(), {_target})
							spawnMob("克苏鲁的仆从", {_eoc}.getLocation(), {_target})
							set {_dashCombo} to 0
					else:
						#dash
						if health of {_eoc} / max health of {_eoc} <= 0.05:
							set {_rage} to -2
							{_attrMap}.put("damage", 80)
						else:
							if {_indexAI} <= 10:
								if {_indexAI} is 0:
									if {_dashCombo} <= ceil(max health of {_eoc} / health of {_eoc}):
										if {_dashCombo} >= 0:
											set {_dVec} to ({_target}.getLocation().subtract({_eoc}.getLocation())).toVector()
											play sound "entity.eoc.growl" with volume 10 and pitch 1 at {_loc}
											{_dVec}.normalize().multiply(max(4, distance between {_target} and {_eoc} / 6))
										add 1 to {_dashCombo}
									else:
										if y-coordinate of {_eoc} < y-coordinate of {_target}:
											set {_dVec} to vector(0, 1, 0)
										else:
											set {_dVec} to vector(0, -1, 0)
										set {_dashCombo} to -2
								{_loc}.add({_dVec})
							else:
								set {_indexAI} to -1
				else:
					#combo dash
					if {_indexAI} <= 8:
						if {_indexAI} is 0:
							set {_dVec} to ({_target}.getLocation().subtract({_eoc}.getLocation())).toVector()
							play sound "entity.eoc.growl" with volume 10 and pitch 1 at {_loc}
							{_dVec}.normalize().multiply(max(4, distance between {_target} and {_eoc} / 6))
						{_loc}.add({_dVec})
					else:
						set {_indexAI} to -1
				add 1 to {_indexAI}
		teleport {_eoc} to {_loc}
		wait 3 ticks
	{_bossbar}.setVisible(false)
	{BOSS}.remove("克苏鲁之眼")
	#drop
	set {_maxHealth} to max health of {_eoc}
	{_maxHealth} is not 1
	broadcast "&d&l克苏鲁之眼 被击败了."
	set {_targets} to metadata "targets" of {_eoc}
	set {_killers} to {_targets}.keySet().iterator()
	set {_drop} to a leather named "&r专家模式福袋" with lore "&7克苏鲁之眼"
	while {_killers}.hasNext():
		set {_ply} to {_killers}.next()
		set {_player} to {_ply} parsed as player
		name of {_player} is {_ply}
		set {_tier} to metadata "tier" of {_player}
		if {_targets}.get({_ply}) * 5 > ({_maxHealth}) / ({_targets}.size()):
			if {_tier} < 1:
				set metadata "tier" of {_player} to 1
				set yml value "stats.tier" from file "plugins/PlayerData/%name of {_player}%.yml" to 1
			if {_player} can hold {_drop}:
				send "&a恭喜你击败了BOSS[&r克苏鲁之眼&a]!您的战利品已经放在背包里了。" to {_player}
				give {_drop} to {_player}
			else:
				send "&a恭喜你击败了BOSS[&r克苏鲁之眼&a]!您的背包空间不足以容纳战利品，请迅速拾起脚下的战利品袋，以免丢失。" to {_player}
				drop {_drop} at {_player}.getEyeLocation() without velocity
		else:
			send "&aBOSS 克苏鲁之眼 已经被击败。很遗憾，您的输出不足以获得一份战利品。" to {_player}